<?xml version="1.0" encoding="utf-8"?>
<base:BaseContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
                    xmlns:base="components.base.*"
                    xmlns:ctrl="components.map.controllers.*"
                    implements="components.map.controllers.IMapViewportController"
                    initialize="this_initializeHandler(event)"
                    resize="this_resizeHandler(event)">
   
   <fx:Script>
      <![CDATA[
      import components.base.viewport.Viewport;

      import models.galaxy.Galaxy;

      import mx.events.FlexEvent;
      import mx.events.ResizeEvent;


      private var _viewport: Viewport;

      public function setViewport(viewport: Viewport): void {
         _viewport = viewport;
         initializeControllers();
      }

      private var f_cleanupCalled:Boolean = false;
      public function cleanup(): void {
         if (f_cleanupCalled) {
            return;
         }
         f_cleanupCalled = true;
         _viewport = null;
         zoom.cleanup();
         watchedObjects.cleanup();
      }

      private function this_initializeHandler(event: FlexEvent): void {
         initializeControllers();
      }

      private function initializeControllers(): void {
         if (zoom != null) {
            zoom.setViewport(_viewport);
         }
         if (watchedObjects != null && watchedObjects.model == null) {
            var galaxy:Galaxy = ML.latestGalaxy;
            watchedObjects.model =
               new WatchedObjects(galaxy, new GalaxySectorsProvider(galaxy));
         }
      }

      private function this_resizeHandler(event: ResizeEvent): void {
         if (watchedObjects != null) {
            watchedObjects.maxHeight = this.height - 110;
            watchedObjects.invalidateSize();
            invalidateSize();
         }
      }
      ]]>
   </fx:Script>
   
   <ctrl:ViewportZoomController id="zoom"/>
   <ctrl:CWatchedObjects
      id="watchedObjects"
      right="2" bottom="2"
      skinClass="components.skins.MapObjectsListSkin">
   </ctrl:CWatchedObjects>

</base:BaseContainer>
