<?xml version="1.0" encoding="utf-8"?>
<base:BaseContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
                    xmlns:s="library://ns.adobe.com/flex/spark"
                    xmlns:mx="library://ns.adobe.com/flex/mx"
                    xmlns:base="components.base.*"
                    width="100%" height="100%"
                    creationComplete="this_creationCompleteHandler()"
                    addedToStage="this_addedToStageHandler(event)">
   
   <fx:Script>
      <![CDATA[
         import controllers.DisplayManager;
         import controllers.screens.Screens;
         import controllers.screens.ScreensSwitch;
         import controllers.startup.StartupInfo;
         import controllers.startup.StartupManager;
         
         import mx.controls.Alert;
         
         import utils.assets.AssetNames;
         import utils.locale.XMLBundlesLoader;
         
         
         private function this_addedToStageHandler(event:Event):void
         {
            DisplayManager.setFPSForStartup();
         }
         
         
         /* ##################### */
         /* ### STARTUP CHECK ### */
         /* ##################### */
         
         
         private function get STARTUP_INFO() : StartupInfo
         {
            return StartupInfo.getInstance();
         }
         
         
         private function this_creationCompleteHandler() : void
         {
            if (STARTUP_INFO.loadSuccessful)
            {
               loadLocaleXML();
            }
            else
            {
               Alert.show("ExternalInterface not available: please upgrade your browser.", "Error!");
            }
         }
         
         
         /* ############### */
         /* ### BUNDLES ### */
         /* ############### */
         
         
         private var _bundlesLoader:XMLBundlesLoader;
         
          
         private function loadLocaleXML() : void
         {
            _bundlesLoader = new XMLBundlesLoader(STARTUP_INFO.locale);
            _bundlesLoader.addEventListener(Event.COMPLETE, bundlesLoader_completeHandler);
            _bundlesLoader.load();
         }
         
         
         private function bundlesLoader_completeHandler(event:Event) : void
         {
            if (_bundlesLoader.loadSuccessful)
            {
               loadImages();
            }
            else
            {
               Alert.show("Unable to load locale data: " + _bundlesLoader.error, "Error!");
            }
            _bundlesLoader = null;
         }
         
         
         /* ############## */
         /* ### IMAGES ### */
         /* ############## */
         
         
         /**
          * Starts downloading images.
          */
         private function loadImages() : void
         {
            IMG.addEventListener(ProgressEvent.PROGRESS, imageLoaded);
            IMG.addEventListener(Event.COMPLETE, imagesLoadComplete);
            IMG.startDownload();
         }
         
         
         /**
          * Updates progress bar when image is downloaded.
          */ 
         private function imageLoaded(event:ProgressEvent) : void
         {
            progress.setProgress(event.bytesLoaded, event.bytesTotal);
         }
         
         
         /**
          * Switch to login screen when download is complete.
          */ 
         private function imagesLoadComplete(event:Event) : void
         {
            DisplayManager.setFPSForGame();
            // Now we can set background image
            (parentApplication as SpaceGame).backgroundImage = IMG.getImage(AssetNames.BACKGROUND_IMAGE);
            IMG.removeEventListener(ProgressEvent.PROGRESS, imageLoaded);
            IMG.removeEventListener(Event.COMPLETE, imagesLoadComplete);
            ScreensSwitch.getInstance().showScreen(Screens.LOGIN);
            StartupManager.initializeApp();
         }
      ]]>
   </fx:Script>
   
   <s:Group verticalCenter="0" horizontalCenter="0" width="200">
      <s:layout>
         <s:VerticalLayout horizontalAlign="center"/>
      </s:layout>
      <s:Label text="Loading images. Please stand by..."/>
      <mx:ProgressBar id="progress" width="100%" height="12" mode="manual"/>
      <mx:Spacer height="16"/>
      <s:Label id="lblFileName" text="{IMG.currentModuleLabel}"/>
   </s:Group>
   
</base:BaseContainer>
